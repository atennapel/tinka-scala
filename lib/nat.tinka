import "lib/fix";

def NatF (R : Type) : Type = Sum R ();
def Nat = Fix NatF;
def Z : Nat = In {F = NatF} (Right []);
def S (n : Nat) : Nat = In {F = NatF} (Left n);

def elimNat
  (P : Nat -> Type)
  (z : P Z)
  (s : (m : Nat) -> P m -> P (S m))
  (n : Nat) : P n
  = elimFix {NatF} P (\rec y. elimSum (\z. P (In {NatF} z)) (\m. s m (rec m)) (\_. z) y) n;

def caseNat {A} (n : Nat) (z : A) (s : Nat -> A) : A = elimNat (\_. A) z (\m _. s m) n;
def cataNat {A} (n : Nat) (z : A) (s : A -> A) : A = elimNat (\_. A) z (\_. s) n;

def add (a b : Nat) : Nat = cataNat a b S;

def prev (n : Nat) : Nat = caseNat n Z (\m. m);
