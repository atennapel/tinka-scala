import "lib/bool";

def Sum (A B : Type) : Type = (tag : Bool) ** if tag A B;
def (:+:) = Sum;

def Left {A B} (x : A) : A :+: B = (True, x);
def Right {A B} (x : B) : A :+: B = (False, x);

def elimSum
  {A B}
  (P : A :+: B -> Type)
  (left : (x : A) -> P (Left x))
  (right : (x : B) -> P (Right x))
  (x : A :+: B) : P x
  = elimBool (\tag. (x : if tag A B) -> P (tag, x)) left right x.tag x._2;

def caseSum {A B R} (x : A :+: B) (left : A -> R) (right : B -> R) : R =
  elimSum (\_. R) left right x;
